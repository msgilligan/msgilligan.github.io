buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'me.champeau.gradle:jbake-gradle-plugin:0.2'
        classpath 'org.jbake:jbake-core:2.4.0'
        classpath 'org.asciidoctor:asciidoctorj:1.5.2'
        classpath 'org.thymeleaf:thymeleaf:2.1.3.RELEASE'
        classpath 'org.thymeleaf.extras:thymeleaf-extras-conditionalcomments:2.1.1.RELEASE'
        classpath 'org.pegdown:pegdown:1.4.2'
    }
}

plugins {
  id "org.ajoberstar.github-pages" version "1.5.1-rc.2"
}

apply plugin: 'me.champeau.jbake'

jbake {
    clearCache = true
}

defaultTasks 'jbake'

def siteDir = "$buildDir/jbake"

githubPages {
//  repoUri = 'git@github.com:msgilligan/msgilligan.github.io.git'
  repoUri = 'https://github.com/msgilligan/msgilligan.github.io.git'
  targetBranch = 'master'

  pages {
     from(file("$siteDir")) {
     	into '.'
     }
  }
  credentials {
    // Use token stored as property for local builds and a travis encrypted key (in 'GH_TOKEN') for Travis builds
    username = project.hasProperty('msgGithubAccessToken') ? msgGithubAccessToken : System.getenv('GH_TOKEN')
    // Apparently password can't be null or '' if githubPages to use "hardcoded" credential in the username.
    password = '\n'
  }
}

publishGhPages.dependsOn jbake

task publish(dependsOn: publishGhPages, group: 'render',
            description: "Publishes buildDir to Github Pages on 'master' branch")
