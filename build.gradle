buildscript {
    repositories {
        jcenter()
        maven { url 'http://dl.bintray.com/aalmiray/asciidoctor' }
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:0.7.0'
        classpath 'org.ajoberstar:gradle-git:0.7.1'
    }
}

apply plugin: 'asciidoctor'
apply plugin: 'github-pages'

def siteDir = "$buildDir/site"

asciidoctor {
    backend = 'html5'   // html5 only for now
//    backends = ['fopub','html5']  // Requires 0.8.0 asciidoctor plugin
    sourceDir = file("adoc")
    outputDir = file("$siteDir")
    options = [
        attributes: [
            icons: 'font',
            'source-highlighter': 'prettify',
            experimental: true,
            copycss: true
        ]
    ]
}

githubPages {
//  repoUri = 'git@github.com:msgilligan/msgilligan.github.io.git'
  repoUri = 'https://github.com/msgilligan/msgilligan.github.io.git'
  targetBranch = 'master'

  pages {
     from(file("$siteDir")) {
     	into '.'
     }
  }
  credentials {
    username = msgGithubAccessToken
//    password = ''
  }
}


task copyStatic(type: Copy,
    group: 'render',
    description: "Copies static files to buildDir") {
    from "$rootDir"
    into "$siteDir"
    include "index.html","README.adoc"
}

task generate(dependsOn: [copyStatic, asciidoctor],
    group: 'render',
    description: "Generates static site in buildDir") {
    doLast {
        println "Static site generated in buildDir"
    }
}

publishGhPages.dependsOn generate

task publish(dependsOn: publishGhPages, group: 'render',
            description: "Publishes buildDir to gh-pages")

//task publish(type: GradleBuild, dependsOn: generate,
//    group: 'render',
//    description: "Publishes buildDir to gh-pages") {
//    buildFile = 'publish.gradle'
//    tasks = ['publishGhPages']
//}

